# Amazon Connect Module

This module can be used to deploy an Amazon Connect instance and all supporting resources, such as Hours of Operation, queues, etc (full list below). It also supports passing in an existing instance ID, and creating supporting resources associated to it. Common deployment examples can be found in the [./examples](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples) directory.

**NOTE: At this time, due to limitations in the Amazon Connect API certain operations are not supported, such as deleting a queue. If you have created these resources with Terraform, and wish to destroy the instance, you must first remove them from the Terraform state with `terraform state rm`.**

**Specifically with queues, if you delete them via Terraform and get a duplicate name error when trying to create them again, you will need to rename or import them into the Terraform state.**

## Usage

The example below is the basic usage of this module and will create an Amazon Connect instance.

```hcl
module "amazon_connect" {
  source  = "aws-ia/amazonconnect/aws"
  version = "~> 0.0.1"

  instance_identity_management_type = "CONNECT_MANAGED"
  instance_inbound_calls_enabled    = true
  instance_outbound_calls_enabled   = true
  instance_alias                    = "my-instance-alias"
}
```

## Usage Examples

* [Simple](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/simple/main.tf)
* [Instance w/ S3 Storage Configuration](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/instance-storage-config-s3/main.tf)
* [Instance w/ Kinesis Storage Configuration](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/instance-storage-config-kinesis/main.tf)
* [Instance w/ Hours of Operations](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/hours-of-operations/main.tf)
* [Instance w/ Queue](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/queue/main.tf)
* [Instance w/ Lex Bot Association](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/lex-bot-association/main.tf)
* [Complete](https://github.com/aws-ia/terraform-aws-amazonconnect/tree/main/examples/complete/main.tf)

## Dependent Resources

Many resources within Amazon Connect have dependencies. A basic example is if you are creating a Queue that depends on an Hour of Operation. If you were not using this module, this would look straightforward:

Without module:

```hcl
resource "aws_connect_hours_of_operation" "example" { ... }

resource "aws_connect_queue" "example" {
  ...
  hours_of_operation_id = aws_connect_hours_of_operation.example.hours_of_operation_id
}
```

With this module, you can do the same thing in a single use of the module. It's possible by using the modules outputs as values for its variables/inputs. At first glance, this might not seem intuitive/possible, but since the Terraform plan phase "flattens" everything to resolve the DAG/order of operations for the deployment, it is completely fine.

With module:

```hcl
module "amazon_connect" {
  ...
  hours_of_operations = {
    example = { ... }
  }
  queues = {
    example = {
      hours_of_operation_id = try(module.amazon_connect.hours_of_operations["example"].hours_of_operation_id, null)
    }
  }
}
```

### Important note for Amazon Connect User Hierarchy Group

The one place where this is not possible is for User Hierarchy Group resources, which have a circular dependency through `parent_group_id`. In the module, resources are created through a single resource combined with a `for_each` loop. Because of this, it would create a circular reference for Terraform to have one iteration reference itself. Instead, if you need a child/parent group relationship to be created, make a second module call for the parent group.

❌ Invalid example:

```hcl
module "amazon_connect" {
  ...
  user_hierarchy_groups = {
    parent = { ... }
    child = {
      parent_group_id = module.amazon_connect.user_hierarchy_groups["parent"].hierarchy_group_id
    }
  }
}
```

✔️ Valid example:

```hcl
module "amazon_connect" {
  ...
  user_hierarchy_groups = {
    child = {
      parent_group_id = try(module.amazon_connect_parent_group.user_hierarchy_groups["parent"].hierarchy_group_id, null)
    }
  }
}

module "amazon_connect_parent_group" {
  ...
  create_instance = false
  instance_id     = module.amazon_connect.instance.id
  user_hierarchy_groups = {
    parent = {}
  }
}
```

## Creating/Exporting Contact Flow JSON

Terraform and the Amazon Connect API expect Contact Flows and Contact Flow Modules to be provided in JSON format. Currently, the easiest way to do that is to first create the Contact Flow in the Amazon Connect management console as desired, and then retrieve the JSON format using the AWS CLI or AWS Tools for PowerShell.

AWS CLI:

```shell
aws connect describe-contact-flow --instance-id <value> --contact-flow-id <value>
aws connect describe-contact-flow-module --instance-id <value> --contact-flow-id <value>
```

AWS Tools for PowerShell

```powershell
Get-CONNContactFlow -ContactFlowId <String> -InstanceId <String>
Get-CONNContactFlowModule -ContactFlowId <String> -InstanceId <String>
```

## Module Outputs

With the exception of `instance_id`, which returns the Amazon Connect Instance ID that was created or passed in, all outputs of this module return the entire resource, or collection or resources. This methodology allows the consumer of the module to access all resource attributes created, but does require some HCL if you'd like to transform it to a different structure.

As an example, if you want to get a list of the queue ARNs:

```hcl
module "amazon_connect" { ... }

locals {
  queue_arns = [ for k, v in module.amazon_connect.queues : v.arn ]
}
```

## License

Apache 2 Licensed. See [LICENSE](./LICENSE) for full details.
